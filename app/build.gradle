plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
    id 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId 'net.rafaeltoledo.filasp'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName '1.0.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        manifestPlaceholders = [
                maps_api_key: GOOGLE_MAPS_API_KEY
        ]
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/distribution/debug.keystore")
        }

        release {
            storeFile file("$rootDir/distribution/release.keystore")
            storePassword System.env.RELEASE_KEY_PASSWORD
            keyAlias 'fila-vacina-sp'
            keyPassword System.env.RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                releaseNotes = file("$rootDir/releasenotes.txt").text
                testers = file("$rootDir/testers.txt").text
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures.viewBinding true

    ndkVersion '22.1.7171670'
}

kapt {
    correctErrorTypes true

    javacOptions {
        // These options are normally set automatically via the Hilt Gradle plugin, but we
        // set them manually to workaround a bug in the Kotlin 1.5.20
        // See https://github.com/google/dagger/issues/2684
        // See https://youtrack.jetbrains.com/issue/KT-47416
        option '-Adagger.fastInit=ENABLED'
        option '-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true'
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$versions.core"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint"
    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.activity:activity-ktx:$versions.activity"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerview"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    implementation "com.google.dagger:hilt-android:$versions.dagger"
    kapt "com.google.dagger:hilt-android-compiler:$versions.dagger"

    implementation "com.github.permissions-dispatcher:permissionsdispatcher:$versions.permission"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:$versions.permission"

    implementation "com.google.android.gms:play-services-maps:$versions.maps"
    implementation "com.google.android.gms:play-services-location:18.0.0"

    implementation 'com.google.firebase:firebase-analytics-ktx:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.1.0'
    implementation 'com.google.firebase:firebase-perf-ktx:20.0.1'

    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}